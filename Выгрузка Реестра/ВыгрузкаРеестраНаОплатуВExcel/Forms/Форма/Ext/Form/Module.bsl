
&НаСервере
Процедура ЗаполнитьРеестраНаСервере()
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	игсЗаявкаНаОплату.Ссылка КАК Заявка,
	               |	игсЗаявкаНаОплату.ВидОплаты КАК ВидОплаты,
	               |	игсЗаявкаНаОплату.СуммаДокумента КАК Сумма,
	               |	игсЗаявкаНаОплату.СуммаНДС КАК СуммаНДС,
	               |	игсЗаявкаНаОплату.ВалютаПлатежа КАК Валюта,
	               |	игсЗаявкаНаОплату.Контрагент КАК Контрагент,
	               |	игсЗаявкаНаОплату.ДоговорКонтрагента КАК Договор,
	               |	игсЗаявкаНаОплату.Инициатор КАК Инициатор
	               |ИЗ
	               |	Документ.игсЗаявкаНаОплату КАК игсЗаявкаНаОплату
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	               |		ПО игсЗаявкаНаОплату.Ссылка = ПлатежноеПоручение.игсСсылкаЗаявкаНаОплату
	               |ГДЕ
	               |	игсЗаявкаНаОплату.Проведен
	               |	И НЕ игсЗаявкаНаОплату.ПометкаУдаления
	               |	И (игсЗаявкаНаОплату.Дата > &ДатаНачала
	               |			ИЛИ &ДатаНачала = ДАТАВРЕМЯ(1, 1, 1))
	               |	И (игсЗаявкаНаОплату.Номер = &Номер
	               |			ИЛИ &Номер = """")
	               |	И (игсЗаявкаНаОплату.Инициатор = &Инициатор
	               |			ИЛИ &Инициатор = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка))
	               |	И (игсЗаявкаНаОплату.Контрагент = &Контрагент
	               |			ИЛИ &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	               |	И (игсЗаявкаНаОплату.Дата < &ДатаОкончания
	               |			ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1))";
	Запрос.УстановитьПараметр("ДатаНачала", ПериодОтбор.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ПериодОтбор.ДатаОкончания); 
	Запрос.УстановитьПараметр("Номер", НомерОтбор);
	Запрос.УстановитьПараметр("Инициатор", ИнициаторОтбор);
	Запрос.УстановитьПараметр("Контрагент", КонтрагентОтбор);
	Результат = Запрос.Выполнить();


	Объект.Заявки.Загрузить(Результат.Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеестра(Команда)
	ЗаполнитьРеестраНаСервере();
КонецПроцедуры

&НаСервере
Функция ПечатьРеестраНаСервере()
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Возврат ДокументОбъект.СФормироватьРеестр();
Конецфункции

&НаКлиенте
Процедура ПечатьРеестра(Команда)
	ОписаниеФайлаРеестра = ВыборФайла();
	
	Если ОписаниеФайлаРеестра = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	ДанныеРеестра = ПечатьРеестраНаСервере();
	ДанныеРеестра.ТабличныйДокумент.Записать(ОписаниеФайлаРеестра.ИмяФайла, ТипФайлаТабличногоДокумента.XLSX);
	Для каждого ВложениеZip Из ДанныеРеестра.Вложения Цикл
		ВложениеZip.Значение.ДвоичныеДанные.Записать(ОписаниеФайлаРеестра.Каталог + "\" + ВложениеZip.Значение.ИмяФайлаАрхива);
	КонецЦикла;	
	ПоказатьПредупреждение(,НСтр("ru = 'Реестр сформирован!';"), 10);
	
КонецПроцедуры

&НаКлиенте
Функция ВыборФайла()
	
	ДиалогФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогФайла.Фильтр = "Реестр (*.xlsx)|*.xlsx";
	
	Если ДиалогФайла.Выбрать() Тогда
		Возврат Новый Структура("ИмяФайла, Каталог",
			ДиалогФайла.ПолноеИмяФайла, ДиалогФайла.Каталог);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ЗаявкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЗаявкиПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры
